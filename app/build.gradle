apply plugin: 'com.android.application'
apply plugin: "net.ltgt.errorprone"

dependencies {
    annotationProcessor "com.uber.nullaway:nullaway:0.9.7"
    errorprone "com.google.errorprone:error_prone_core:2.4.0"
    //noinspection GradleDynamicVersion
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"

    implementation 'androidx.activity:activity:1.5.0'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20210307'
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}


tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone {
//            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.uber")
        }
    }
}
//signing.properties looks like this:
//storeFileName=googleplay.jks
//storeAlias=valle
//storePassword=password
//keyPassword=password
if (file("./signing.properties").exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("signing.properties")))
    android {
        signingConfigs {
            release {
                storeFile file(props['storeFileName'])
                keyAlias props['storeAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }
//        to run tests use "gradlew clean uninstallAll connectedAndroidTest"
//        comment testBuildType out to run debug build tests: "gradlew clean uninstallAll connectedDebugAndroidTest"
        testBuildType "release"
        buildTypes {
            release {
                minifyEnabled true
                zipAlignEnabled true
                proguardFile './proguard.cfg'
                testProguardFile './proguard.cfg'
                signingConfig signingConfigs.release
            }
        }
    }
}

android {
    compileSdkVersion 31
    defaultConfig {
        versionCode 350
        versionName "3.5.0"
        applicationId "ru.valle.btc"
        testApplicationId "ru.valle.btc.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        targetSdkVersion 31
        minSdkVersion 14
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '31.0.0'
}
